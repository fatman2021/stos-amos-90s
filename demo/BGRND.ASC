10 default : goto 50
20 ink 0 : bar X(X),Y(X) to X(X),Y(X)+YDIR(X) : Y(X)=YDIR(X)+Y(X) : ink COL(X) : bar X(X),Y(X) to X(X),Y(X)+YDIR(X) : return 
50 MX=5
55 mode 0
60 gosub 330
70 if D=1 then end 
80 gosub 600
90 default : end 
100 rem ~~~ Change star position ~~~~~~~
110 rem ~~~ Main loop ~~~~~~~~~~~~~~~~~
120 repeat 
130 STRTX=rnd(320) : STRTY=rnd(200)
140 TME=rnd(40)+30
150 for A=1 to TME
160 if mouse key>0 then A=TME
170 if fkey=3 then gosub 990
180 for X=1 to MX : gosub 20 : next X
190 next A : if FL then return 
200 until mouse key>0
210 return 
220 rem ~~~ If off screen, set to centre
230 resume 260
240 rem ~~~ Set direction of star
250 rem ~~~ And put at centre
260 inc COL(X) : X(X)=rnd(320) : Y(X)=0 : YDIR(X)=rnd(4)+1 : COL(X)=COL(X) mod 16 : return 
320 rem ~~~ Titles ~~~~~~~~~~~~~~~~~~~~
330 windopen 10,0,0,40,12,10,3
340 clw : inverse on 
350 print : centre "Fireworks V1.1"
360 inverse off 
370 print : print 
380 A$=chr$(189)+" Matthew J. Waller 1991"
385 centre A$
390 print : print 
400 centre "06-08-1991"
420 print : print 
430 centre "Press any key to continue"
440 print 
450 centre "Any mouse button will terminate"
460 while D=0 : D=fkey
470 if D=1 then put key "frequency:goto80`"
480 if D=10 then FL=true
490 wend 
500 return 
510 rem ~~~ Main setup ~~~~~~~~~~~~~~~~~
520 Q=4
530 curs off : key off 
540 palette $0,$171,$272,$373,$474,$374,$75,$77,$337,$727,$724,$722,$752,$772,$774,$777
550 change mouse Q
560 clw : auto back off 
570 dim COL(MX),X(MX),Y(MX),XDIR(MX),YDIR(MX)
580 on error goto 230
590 return 
600 windel 10
610 if FL then gosub 750 else gosub 520 : gosub 940 : gosub 120
620 return 
630 rem ~~~ Structure ~~~~~~~~~~~~~~~~
640 rem a-goto titles
650 rem b-setup
660 rem c-main loop
670 rem d-end
680 rem ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
690 rem ~~~ Main loop structure ~~~~~~
700 rem a-change central position
710 rem b-move stars outward
720 rem c-move a star or put in centre
730 rem ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
740 rem ~~~ Setup for the time test ~~
750 timer=0
760 TME=100
770 STRTX=160 : STRTY=100
780 MX=50
790 gosub 520
800 FL=true
810 print "Testing program speed ..."
820 gosub 940
830 timer=0
840 gosub 150
850 X=timer
860 print "Test complete."
870 print "Time taken = ";X;" cycles (50ths"
880 print "of a second)"
890 print "This is ";X/50.0;" seconds"
900 print "Press any key."
910 wait key 
920 return 
930 rem ~~~ launch all ~~~~~~~~~~~~~~~~
940 for X=1 to MX
950 gosub 260
960 next X
970 return 
980 rem reverse directions
990 for X=1 to MX
1000 XDIR(X)=-XDIR(X) : YDIR(X)=-YDIR(X) : next X : return 
