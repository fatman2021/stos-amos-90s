5 auto back off 
10 rem create cover for diary
20 mode 0 : key off : flash off 
25 curs off 
30 hide on 
40 rem PALLETE
50 rem 0= BGROUND
60 rem 1= GOLD
70 rem 2= LIGHT GLD
80 rem 3= DRK GLD
90 rem 4-13 = VARIATIONS OF BROWN
100 rem 14 + 15 = PAPER
130 rem NOW THE DRAW BIT
140 rem SPRITEBANK NEEDED WITH ALL
150 rem NUMBERS (0 - 9)
160 rem BUT THIS WILL COME LATER
170 rem
180 rem A BOX TO WORK IN
190 ink 7 : YM=15 : XM=-15
200 LX=47 : RX=235 : LY=1 : RY=178
210 bar LX,LY to RX,RY
220 rem EXPERIMENTATION
230 X1=LX+(rnd(RX-LX))
240 X2=LX+(rnd(RX-LX))
250 Y1=LY+(rnd(RY-LY))
260 Y2=LY+(rnd(RY-LY))
265 repeat 
270 DX1=rnd(6)-3
280 DY1=rnd(6)-3
290 DX2=rnd(6)-3
300 DY2=rnd(6)-3
310 gr writing 2+rnd(1) : ink rnd(7)+6
320 for POW=1 to rnd(30)+50
330 X1=X1+DX1 : X2=X2+DX2 : Y1=Y1+DY1 : Y2=Y2+DY2
340 rem CHK
350 if (X1<LX) or X1>RX then X1=X1-DX1 : DX1=-DX1
360 if (X2<LX) or X2>RX then X2=X2-DX2 : DX2=-DX2
370 if Y1<LY or Y1>RY then Y1=Y1-DY1 : DY1=-DY1
380 if Y2<LY or Y2>RY then Y2=Y2-DY2 : DY2=-DY2
390 draw X1,Y1 to X2,Y2
400 next POW
410 until mouse key or FLAG
415 gr writing 1
420 rem THE FINAL COAT
425 rem goto shape book
426 gosub 1000
430 rem draw all surrounding book
435 gr writing 2
440 for G=RY+1 to RY+YM-2
450 ink rnd(1)+14
460 draw RY-G+LX+1,G to RY-G+RX-3,G
470 next G
471 A$=screen$(back,0,0 to 319,199)
474 ink 6
475 bar LX+XM,LY+YM to RX+XM,RY+YM
476 screen$(logic,0,0)=A$
477 screen$(back,0,0)=A$
478 screen$(physic,0,0)=A$
479 save "diarycov.neo",back
480 end 
999 rem scroll book to fit a book shape
1000 def scroll 1,LX,LY to RX+YM,RY+YM,-1,0
1010 def scroll 2,LX,LY to LX+1,RY+YM,0,1
1020 for G=1 to YM
1030 scroll 1
1035 scroll 2
1040 next G
1050 RX=RX-YM : screen copy physic to back
1060 return 
