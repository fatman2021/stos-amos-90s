5 key off : clw : click off : voice on 1 : voice on 2 : voice on 3 : volume 10
6 dim V(3) : rem voices currently playing
7 NV=1
10 open #1,"MIDI"
15 FL=false
16 repeat : until asc(input$(#1,1))=254 : rem clear midi
20 repeat
30 gosub 200
35 if P=36 then FL=true
40 until FL
45 click on
50 end
200 R=asc(input$(#1,1)) : gosub 1000
210 rem plot R,X : X=X+1 : if X>199 then X=0 : clw
211 rem if R<>254 then print R
215 if R=254 then timer=0
220 return
1000 rem mode based midi info processing
1010 rem m=mode
1020 rem r=info
1030 if R=254 then M=0
1040 if R=144 then M=1 : return
1050 if M=1 then gosub 1200 : return
1060 if M=2 then gosub 1400 : return
1070 return
1200 rem mode 1, so info is a note
1210 P=R : rem remember which note
1220 M=2 : rem now get volume
1230 return
1400 rem mode 2, so get volume and play or not
1410 if R>0 then gosub 1500 else gosub 1600
1420 M=1
1430 return
1500 rem play note on spare voice
1501 if P<12 or P>(96+11) then return
1502 for T=1 to 3 : if V(T)=0 then NV=T
1503 next T : rem find spare voice if there is one
1510 play NV,P-11,0
1520 V(NV)=P : NV=NV+1 : if NV>3 then NV=1
1530 return
1600 rem turnoff note on voice ??
1610 for T=1 to 3
1620 if V(T)=P then play T,0,0 : V(T)=0
1630 next T
1640 return
