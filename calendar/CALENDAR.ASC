4 fade 4
5 auto back off 
6 logic=back
7 back=default back : physic=default physic
8 ink 1
9 reserve as screen 8
10 rem PLEASE READ THIS PROGRAM IN MEDIUM RESOLUTION
30 rem MATTHEW WALLER'S CALENDAR - FOR THE VERY FORGETFUL PEOPLE
35 rem credits included in program
36 rem DO NOT TAMPER WITH THIS PROGRAM
40 rem
70 key off : curs off : hide on 
90 rem do credits later
100 dim YR(50),STD(50),DIF(13),TIM(13)
130 rem 1=mon 2=tues 3=weds 4=thurs 5=fri 6=sat 7=sun
135 YEAR=1991
136 LP=30
140 gosub 2100
180 if P>Q then DATE=P else DATE=Q
190 rem display the options and months
200 clw : if mode=0 then wait 10 : mode 1
205 restore 250
210 for R=1 to 12
220 box R*48-40,22 to R*48,32
230 set zone R,R*48-40,22 to R*48,32
235 read A$ : locate R*6-4,3 : print A$
240 next R
250 data "JAN","FEB","MAR","APR","MAY","JUN","JUL","AUG","SEP","OCT","NOV","DEC"
260 box 4,6 to 46,16 : set zone 13,4,6 to 46,16 : locate 1,1 : print "EXIT"
270 for H=1 to 4
280 box 96+H*67,6 to 159+H*67,16
285 set zone H+13,96+H*67,6 to 159+H*67,16
290 next H
300 locate 38,1 : print "> > >"
310 locate 22,1 : print "< < <"
320 locate 46,1 : print "Credits"
325 gosub 2000
326 screen copy back to physic : auto back on : logic=physic
327 fade 3,0,$777,$700,$444 : show on : flash 3,"(666,5)(555,5)(444,5)(333,5)(444,5)(555,5)"
330 if YEAR>9999 then LP=29 else LP=30
331 if YEAR>99999 then YEAR=99999
332 if YEAR<-9999 then YEAR=-9999
333 locate LP,1 : print YEAR;" "
334 locate 0,23 : if YEAR<0 then centre "I am not responsible for negative year values" else print space$(78);
340 if MNTH>0 and MNTH<13 then gosub 600
350 T=zone(0) : MK=mouse key
360 if T>0 and T<13 and MK>0 then MNTH=T : goto 340
370 if T=13 and MK>0 then fade 3 : wait 21 : default : end 
380 if T=14 and MK>0 then dec YEAR : goto 330
390 if T=16 and MK>0 then inc YEAR : goto 330
400 if T=15 and MK>0 then gosub 900 : goto 330
410 if T=17 and MK>0 then gosub 1500 : goto 340 : rem credits
420 rem
490 goto 350
600 rem offset
601 if XX>0 then ink 0 : box XX*48-41,21 to XX*48+1,33 : ink 1 : box XX*48-40,22 to XX*48,32
604 ink 1
605 OF=5
606 box MNTH*48-41,21 to MNTH*48+1,33 : XX=MNTH
607 ink 0 : box MNTH*48-40,22 to MNTH*48,32
610 for G=0 to MNTH-1 : OF=OF+DIF(G) : next G
620 MD=TIM(MNTH) : if YEAR mod 4=0 then LEAP=-1 else LEAP=0
630 if LEAP and MNTH>2 then OF=OF+1
640 if LEAP and MNTH=2 then MD=MD+1
650 rem year offset???
660 YO=0
670 YO=YO+(YEAR-1)/4+YEAR
680 OF=(YO+OF) mod 7
685 DCOUNT=0
690 for DAY=0 to 34 : inc DCOUNT
700 I=(OF+DAY) mod 7 : J=(OF+DAY)/7 mod 5
710 locate I*10+4,J*3+7 : if DCOUNT<=MD then print DCOUNT;" "; else print space$(3);
720 next DAY
730 return 
900 locate 29,1 : print space$(6); : locate 29,1
905 clear key : curs on : flash off : palette ,,$700
910 set curs 3,4
915 INPU$="" : LI=0
920 repeat 
930 repeat : X$=inkey$ : until X$=chr$(13) or (asc(X$)>47 and asc(X$)<58)
940 if X$<>chr$(13) then INPU$=INPU$+X$ : print X$; : inc LI
950 until LI=5 or X$=chr$(13)
960 YEAR=val(INPU$)
970 curs off : locate 29,1 : print space$(6);
980 flash 3,"(666,5)(555,5)(444,5)(333,5)(444,5)(555,5)"
990 return 
1500 screen copy back to 8
1505 windopen 2,13,3,54,8,5,3
1510 title "Calendar V1.1"
1520 B$=chr$(189)+" 1991 Matthew Waller" : centre B$ : print : print : centre "Created with STOS"
1530 print : centre "17th October 1991"
1540 print : print : centre "Press anything" : curs off 
1550 repeat : until inkey$<>"" or mouse key>0
1560 windel 2 : screen copy 8 to back : screen copy back to physic
1570 return 
2000 for G=1 to 7
2010 box G*80-75,37 to G*80+5,170 : next G
2020 draw 5,50 to 563,50
2030 for G=1 to 7 : read P$
2040 locate G*10-9,5 : print P$
2050 next G
2060 data "Monday","Tuesday","Wednesday","Thursday","Friday","Saturday","Sunday"
2070 for G=0 to 5
2080 draw 5,G*24+50 to 563,G*24+50 : next G
2090 return 
2100 restore 2140
2105 for G=1 to 13
2110 read DIF(G),TIM(G)
2120 next G
2130 return 
2140 data 3,31
2150 data 0,28
2160 data 3,31
2170 data 2,30
2180 data 3,31
2190 data 2,30
2200 data 3,31
2210 data 3,31
2220 data 2,30
2230 data 3,31
2240 data 2,30
2250 data 3,31
2260 data 1,365
9999 DAYTUESDAYWEDNESDAYTHURSDAYFRIDAYSATURDAYSUNDAY
